apply plugin: 'application'

mainClassName = "com.commercehub.dbratchet.cmdline.Main"
applicationName = "db-ratchet"
def applicationAlias = "ratchet"

def os = System.getProperty("os.name").toLowerCase()
def appInstallPath = ''
if (os.contains("windows")) {
    appInstallPath = 'D:/db-ratchet'
} else if (os.contains("mac os")) {
    appInstallPath = "${System.getProperty('user.home')}/db-ratchet"
} else {
    appInstallPath = './db-ratchet'
}

installApp{
    destinationDir = file(appInstallPath)
}

task createShorthandScritpAliases  {
    dependsOn startScripts
    def scripts = file("${buildDir}/scripts")
    def shortHandScripts = file("${buildDir}/scripts-short")
    outputs.dir shortHandScripts
    doLast {
        copy {
            from scripts
            into shortHandScripts
            include "${applicationName}"
            include "${applicationName}.bat"
            rename { fileName ->
                fileName.replace(applicationName, applicationAlias)
            }
        }
    }
}

applicationDistribution.from(createShorthandScritpAliases) {
    into "bin"
}

dependencies {
	compile project(':dbratchet-core')
    compile 'commons-cli:commons-cli:1.2'
}

